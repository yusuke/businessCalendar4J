plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}

group 'one.cafebabe'
version '1.21'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

compileJava   {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

compileTestJava {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

dependencies {
    compileOnly 'org.slf4j:slf4j-api:2.0.2'
    compileOnly 'org.jetbrains:annotations:23.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

test {
    useJUnitPlatform()
}

jar {
    from('module-info/build/classes/java/main') {
        include "module-info.class"
        into "META-INF/versions/9"
    }
    manifest {
        attributes 'Multi-Release': 'true'
    }
}
tasks.jar.dependsOn([':module-info:compileJava'])

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'businessCalendar4j'
            from components.java
            pom {
                name = 'businessCalendar4j'
                description = 'businessCalendar4j'
                url = 'https://github.com/yusuke/businessCalendar4j'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yusuke'
                        name = 'Yusuke Yamamoto'
                        email = 'yusuke@mac.com'
                    }
                }
                scm {
                    url = 'https://github.com/yusuke/businessCalendar4j'
                    connection = 'scm:git:git://github.com/yusuke/businessCalendar4j.git'
                    developerConnection = 'scm:git:git@github.com:yusuke/businessCalendar4j.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.properties['SONATYPE_USERNAME'] ?: "none"
                password = project.properties['SONATYPE_PASSWORD'] ?: "none"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
